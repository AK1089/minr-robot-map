# FUNCTION | clickOnRobot(Player player, Entity entity, Int index)
@global_cooldown 10
@using akrobots

# editing the armour stand to move it around shouldn't do anything
@if player.getItemInMainHand().getItemType().toLowerCase() == "flint"
    @player &7[&cInfo&7] &eManually overriding for edit mode.
    @var currentlyRunning[index] = false
    @var levelComplete[index] = false
    @return
@fi

# using the emergency stop item
@if player.getItemInMainHand().getItemType().toLowerCase() == "flint"
    @player &7[&cInfo&7] &eUsing the &dEMERGENCY STOP&e...
    @var currentlyRunning[index] = false
    @delay 10
    @define String startPosition = String["-12636.50 115.00 -12526.50 -90 0"][0]
    @bypass /execute as {{entity.getUniqueId()}} at @s run teleport @s {{startPosition}}
    @bypass /minr restore claude_level_{{index}} Theta
    @bypass /data modify entity @e[tag=claude_level_{{index}}_check,limit=1] Items set value []
    @bypass /data modify entity {{entity.getUniqueId()}} HandItems[0].id set value "diamond_pickaxe"
    @delay 10
    @player &7[&cInfo&7] &eTerminated the current script and reset Claude.
    @return
@fi

# already finished this level
@if levelComplete[index]
    @player &7[&cInfo&7] &eYou have already completed this level!
    @return
@fi

# no multiple concurrent runs!
@if currentlyRunning[index]
    @player &7[&cInfo&7] &eClaude is currently executing a script! Please wait for him to finish, or use the emergency stop.
    @return
@fi

# gets the book, which should be in the player's main hand
@define Item book = player.getItemInMainHand()

# if the player isn't holding the book
@if book == null || book.getItemType().toLowerCase() != "writable_book"
    @player &7[&cInfo&7] &ePlease hold a writable book containing your script for Claude. Do not sign the book!
    @return
@fi

# player has now submitted a book
@player &bCompiling robot script...

# parse the script from the writable book
@define Script script = parseScript(player, book)
@var script.robot = Robot(entity, index)
@var script.robot.reset()

@var currentlyRunning[index] = true

# get the main routine
@if !script.execute("main", 0)
    @var script.robot.reset()
    @return
@fi


@player &7[&cInfo&7] &eVerifying completion...
@delay 20
@var levelComplete[index] = checkSolution(player, entity, index)

# check if the level is complete
@if levelComplete[index]
    @player &7[&cInfo&7] &aLevel {{index}} complete - well done!
    @delay 40
    @player &7[&cInfo&7] &eGo talk to the &dForeman &eagain for the next set of instructions.
@else
    @player &7[&cInfo&7] &cLevel failed. Resetting... 
@fi

# reset the robot when done
@var script.robot.reset()
